#rebuild: "tween"
#googleFont: "roboto"
#rebuild: "tween"

COLORSTART = $FF0000
COLOREND = $000088
DRAWWIDTH = 500 : DRAWHEIGHT = 62
TWEENWIDTH = DRAWWIDTH + 32 : TWEENHEIGHT = DRAWHEIGHT + 24
TWEENX = 155 : TWEENY = 32 : TWEENCOUNT = 0

Dim TWEENS@( 100 ), TWEENSCOLOR@( 100 ), TWEENSX( 100 ), TWEENSY( 100 ), TWEENSTITLE$( 100 ), TWEENSTYPE$( 100 ), TWEENSVALUE#( 100 )
Global DRAWWIDTH, DRAWHEIGHT, TWEENWIDTH, TWEENHEIGHT, TWEENX, TWEENY, TWEENCOUNT, CURRENTTWEENS, COUNT
Global TWEENS@(), TWEENSCOLOR@(), TWEENSX(), TWEENSY(), TWEENSTITLE$(), TWEENSTYPE$(), TWEENSVALUE#()

Set Font "Roboto", 20
Text 960, 10, "AOZ Studio Tween Demo!", "#center #top"
Set Font "Roboto", 12

COUNT = 11 * 3
NEWTWEEN[ 0, DRAWHEIGHT, COLORSTART, COLOREND, "Linear", DRAWWIDTH, "number" ]
NEWTWEEN[ 0, DRAWHEIGHT, COLORSTART, COLOREND, "Quadratic", DRAWWIDTH, "number" ]
NEWTWEEN[ 0, DRAWHEIGHT, COLORSTART, COLOREND, "Cubic", DRAWWIDTH, "number" ]
NEWTWEEN[ 0, DRAWHEIGHT, COLORSTART, COLOREND, "Quartic", DRAWWIDTH, "number" ]
NEWTWEEN[ 0, DRAWHEIGHT, COLORSTART, COLOREND, "Quintic", DRAWWIDTH, "number" ]
NEWTWEEN[ 0, DRAWHEIGHT, COLORSTART, COLOREND, "Sinusoidal", DRAWWIDTH, "number" ]
NEWTWEEN[ 0, DRAWHEIGHT, COLORSTART, COLOREND, "Exponential", DRAWWIDTH, "number" ]
NEWTWEEN[ 0, DRAWHEIGHT, COLORSTART, COLOREND, "Circular", DRAWWIDTH, "number" ]
NEWTWEEN[ 0, DRAWHEIGHT, COLORSTART, COLOREND, "Elastic", DRAWWIDTH, "number" ]
NEWTWEEN[ 0, DRAWHEIGHT, COLORSTART, COLOREND, "Back", DRAWWIDTH, "number" ]
NEWTWEEN[ 0, DRAWHEIGHT, COLORSTART, COLOREND, "Bounce", DRAWWIDTH, "number" ]

TIMESTART = TIMER
Repeat

	For T = 0 To COUNT - 1
		DRAWTWEEN[ T ]
	Next T

	K$ = Inkey$
	Wait Vbl
	Inc X

Until K$ = Chr$( 27 )

Procedure NEWTWEEN[ IN, OUT, COLORIN, COLOREND, EASING$, DURATION, TYPE$ ]

	TWEENS@( TWEENCOUNT ) = Tween( Value = IN, ValueEnd = OUT, Duration = ( DURATION * 16.66 ) / 1000, Easing$ = EASING$, InOut$ = "In" )
	TWEENSCOLOR@( TWEENCOUNT ) = Tween( Color = COLORIN, ColorEnd = COLOREND, Duration = ( DURATION * 16.66 ) / 1000, Easing$ = EASING$, InOut$ = "In" )
	DUMMY@ = Tween( Variable = Varptr( TWEENSVALUE#( TWEENCOUNT ) ), Value = 0, ValueEnd = 100, Duration = ( DURATION * 16.66 ) / 1000, Easing$ = EASING$, InOut$ = "In" )

	TWEENSX( TWEENCOUNT ) = TWEENX + ( TWEENCOUNT Mod 3 ) * TWEENWIDTH
	TWEENSY( TWEENCOUNT ) = TWEENY + Int( TWEENCOUNT / 3 ) * TWEENHEIGHT
	TWEENSTITLE$( TWEENCOUNT ) = EASING$ + " / In"
	TWEENSTYPE$( TWEENCOUNT ) = TYPE$
	Inc TWEENCOUNT

	TWEENS@( TWEENCOUNT ) = Tween( Value = IN, ValueEnd = OUT, Duration = ( DURATION * 16.66 ) / 1000, Easing$ = EASING$, InOut$ = "Out" )
	TWEENSCOLOR@( TWEENCOUNT ) = Tween( Color = COLORIN, ColorEnd = COLOREND, Duration = ( DURATION * 16.66 ) / 1000, Easing$ = EASING$, InOut$ = "Out" )
	DUMMY@ = Tween( Variable = Varptr( TWEENSVALUE#( TWEENCOUNT ) ), Value = 0, ValueEnd = 100, Duration = ( DURATION * 16.66 ) / 1000, Easing$ = EASING$, InOut$ = "Out" )
	TWEENSX( TWEENCOUNT ) = TWEENX + ( TWEENCOUNT Mod 3 ) * TWEENWIDTH
	TWEENSY( TWEENCOUNT ) = TWEENY + Int( TWEENCOUNT / 3 ) * TWEENHEIGHT
	TWEENSTITLE$( TWEENCOUNT ) = EASING$ + " / Out"
	TWEENSTYPE$( TWEENCOUNT ) = TYPE$
	Inc TWEENCOUNT

	TWEENS@( TWEENCOUNT ) = Tween( Value = IN, ValueEnd = OUT, Duration = ( DURATION * 16.66 ) / 1000, Easing$ = EASING$, InOut$ = "InOut" )
	TWEENSCOLOR@( TWEENCOUNT ) = Tween( Color = COLORIN, ColorEnd = COLOREND, Duration = ( DURATION * 16.66 ) / 1000, Easing$ = EASING$, InOut$ = "InOut" )
	DUMMY@ = Tween( Variable = Varptr( TWEENSVALUE#( TWEENCOUNT ) ), Value = 0, ValueEnd = 100, Duration = ( DURATION * 16.66 ) / 1000, Easing$ = EASING$, InOut$ = "InOut" )
	TWEENSX( TWEENCOUNT ) = TWEENX + ( TWEENCOUNT Mod 3 ) * TWEENWIDTH
	TWEENSY( TWEENCOUNT ) = TWEENY + Int( TWEENCOUNT / 3 ) * TWEENHEIGHT
	TWEENSTITLE$( TWEENCOUNT ) = EASING$ + " / InOut"
	TWEENSTYPE$( TWEENCOUNT ) = TYPE$
	Inc TWEENCOUNT

End Proc

Procedure DRAWTWEEN[ NUMBER ]


	Colour 31, TWEENSCOLOR@( NUMBER ) Value# : Ink 31
	WBAR = 16
	Bar TWEENSX( NUMBER ) + 1, TWEENSY( NUMBER ) + 1, TWEENWIDTH - 2, WBAR

	Ink 2 : Box TWEENSX( NUMBER ), TWEENSY( NUMBER ), TWEENWIDTH, TWEENHEIGHT
	Gr Writing 0
	Fix 2
	Ink 1 : Text TWEENSX( NUMBER ) + TWEENWIDTH / 2, TWEENSY( NUMBER ) + 14, TWEENSTITLE$( NUMBER ) + " - " + Str$( TWEENSVALUE#( NUMBER ) ), "#center"

	VALUE = TWEENS@( NUMBER ) Value#
	POSITION = ( TWEENS@( NUMBER ) Elapsed# ) * DRAWWIDTH
	X = TWEENSX( NUMBER ) + 16 + POSITION
	Y = TWEENSY( NUMBER ) + 20 + DRAWHEIGHT - VALUE
	Ink 2 : Plot TWEENSX( NUMBER ) + 16 + POSITION, TWEENSY( NUMBER ) + 20 + DRAWHEIGHT - VALUE
	Draw X - 1, Y To X + 1, Y
	Draw X, Y - 1 To X, Y + 1

End Proc
