LANG$ = "en" : CURRENT_PAGE$ = "user_guide/table_of_contents/000_chapter.txt" : CURRENT_CODE$ = ""
ROOT_URL$ = "https://doc.aoz.studio" : CUR_ID$ = "" : CUR_TYPE$ = ""

Dim HISTO_PAGE$( 1000 ) : CUR_HISTO = 1 : CHEAT_ACCESS = False
Global CUR_ID$, CUR_TYPE$, ROOT_URL$, LANG$, CURRENT_PAGE$, HISTO_PAGE$(), CUR_HISTO, CURRENT_CODE$, CHEAT_ACCESS

// Point de départ du programme
Procedure MAIN
	If LOCAL = True Then ROOT_URL$ = "http://localhost/aozdoc/"
	Show HTML "index"
	JS Execute "initPanels();"
	RELOAD_LANG
End Procedure

// Rechargement des rubriques et de la page actuelle avec la langue en cours
Procedure RELOAD_LANG
	StackEdit Open URL$ = ROOT_URL$ + "/php/index.php?page=topics.txt", OnSuccess$="_ON_TOPICS_LOADED"
	If URL_SEARCH$ <> ""
		KEY_SEARCH[ "Enter", URL_SEARCH$, True ]
		URL_SEARCH$ = ""
	Else
		PAGE_LOAD[ "", CURRENT_PAGE$, False ]
	End if
End Procedure

Procedure GO_HOME
	CURRENT_PAGE$ = "user_guide/table_of_contents/000_chapter.txt"
	PAGE_LOAD[ "", CURRENT_PAGE$, True ]
End Procedure

// Chargement d'une page
Procedure PAGE_LOAD[ ID$, URL$, IS_NEW ]
	CURRENT_PAGE$ = URL$
	If IS_NEW = True
		CUR_HISTO = CUR_HISTO + 1
		For R = CUR_HISTO To 1000 : HISTO_PAGE$( R ) = "" : Next R
	End If

	HISTO_PAGE$( CUR_HISTO ) = URL$
	If ID$ <> ""
		If CUR_ID$ <> "" And CUR_ID$ <> ID$
			HTML Element CUR_ID$, ClassName$ = CUR_TYPE$
		End If
		CUR_ID$ = ID$ : CUR_TYPE$ = HTML Element Attribute$( ID$, "class" )
		HTML Element CUR_ID$, ClassName$ = CUR_TYPE$ + " select-topic"
	End If
	StackEdit Open URL$ = ROOT_URL$ + "/php/index.php?page=" + URL$, OnSuccess$="_ON_PAGE_LOADED"
End Procedure

Procedure LOGIN
	JS Execute "document.getElementById( 'log_pwd' ).value = '';"
	JS Execute "document.getElementById( 'frm_login' ).style.display = 'block';"

End Procedure

Procedure KEY_LOGIN[ CKEY$, VALUE$ ]
	If CKEY$ = "Enter" And VALUE$ = "abracadabra"
		JS Execute "document.getElementById( 'md_login' ).style.display = 'none';"
		JS Execute "document.getElementById( 'md_gator' ).style.display = 'block';"
		JS Execute "document.getElementById( 'frm_login' ).style.display = 'none';"
		JS Execute "document.getElementById( 'md_edit' ).style.display = 'block';"
		LANG$ = "en" : RELOAD_LANG
	End If
End Procedure

Procedure PAGE_EDIT
	StackEdit Edit CODE$ = CURRENT_CODE$, OnChange$ = "_CHANGE_EDIT", OnClose$ = "_CLOSE_EDIT"
End Procedure

Procedure _CHANGE_EDIT[ CODE$ ]
	If CODE$ <> CURRENT_CODE$
		CURRENT_CODE$ = CODE$
		JS Execute "document.getElementById( 'md_sync' ).style.display = 'block';"
	End If
End Procedure

Procedure _CLOSE_EDIT[ CODE$ ]
	_ON_PAGE_LOADED[ CODE$ ]
End Proc

// Les rubriques sont chargées ? Alors on les affiche
Procedure _ON_TOPICS_LOADED[ CODE$ ]
	CODE$ = Replace$(CODE$, "procedure:PAGE_LOAD(", "javascript:application.aoz.runProcedure( 'PAGE_LOAD'," )
	CODE$ = Replace$(CODE$, "md/documentation/", "" )
	HTML Element "topics", Content$=CODE$
End Procedure

// La page est chargée . Alors on l'affiche
Procedure _ON_PAGE_LOADED[ CODE$ ]
	CURRENT_CODE$ = CODE$
	StackEdit Show CODE$, "page"
End Procedure

// Page précédente
Procedure PREVIOUS_PAGE
	If CUR_HISTO > 1
		CUR_HISTO = CUR_HISTO - 1
		If MID$( HISTO_PAGE$( CUR_HISTO ), 0, 7 ) = "SEARCH:"
			//CUR_HISTO = CUR_HISTO - 1
			KEY_SEARCH[ "Enter", MID$( HISTO_PAGE$( CUR_HISTO ), 7, LEN( HISTO_PAGE$( CUR_HISTO ) ) ), False ]
		Else
			PAGE_LOAD[ "", HISTO_PAGE$( CUR_HISTO ), False ]
		End If
	End If
End Procedure

// Page suivante
Procedure NEXT_PAGE
	If CUR_HISTO < 1000 And HISTO_PAGE$( CUR_HISTO + 1 ) <> ""
		CUR_HISTO = CUR_HISTO + 1
		If MID$( HISTO_PAGE$( CUR_HISTO ), 0, 7 ) = "SEARCH:"
			//CUR_HISTO = CUR_HISTO - 1
			KEY_SEARCH[ "Enter", MID$( HISTO_PAGE$( CUR_HISTO ), 7, LEN( HISTO_PAGE$( CUR_HISTO ) ) ), False ]
		Else
			PAGE_LOAD[ "", HISTO_PAGE$( CUR_HISTO ), False ]
		End If
	End If
End Procedure

Procedure KEY_SEARCH[ CKEY$, VALUE$, ISNEW ]
	If CKEY$ = "Enter"
		If Lower$(VALUE$) = "lend me your pen"
			CHEAT_ACCESS = True
			JS Execute "document.getElementById( 'md_login' ).style.display = 'block';"
			JS Execute "document.getElementById( 'md_gator' ).style.display = 'none';"
		Else
			If ISNEW
				If CUR_HISTO < 1000
					CUR_HISTO = CUR_HISTO + 1
					For R = CUR_HISTO To 1000 : HISTO_PAGE$( R ) = "" : Next R
				End If
			End If
			HISTO_PAGE$( CUR_HISTO ) = "SEARCH:" + VALUE$
			JS Execute "search( '" + VALUE$ + "' );"
		End if
	End If
End Procedure

Procedure PAGE_SAVE
	StackEdit Save CURRENT_PAGE$, CURRENT_CODE$, OnSuccess$="_ON_PAGE_SAVED"
End Procedure

Procedure _ON_PAGE_SAVED[ MESSAGE$ ]
	JS Execute "document.getElementById( 'md_sync' ).style.display = 'none';"
	Toast "Page saved!", Type$="success"
End Procedure
